#import "../../../src/contracts/cameligo/oracle/types.mligo" "Types"
#import "../../../src/contracts/cameligo/oracle/callback/main.mligo" "Callback"
#import "callback.jsligo" "Helper_Callback"

const plain_timestamp : timestamp = ("1970-01-01T00:00:01Z" as timestamp);

const primary_event : Types.event_type =
    {
        name : "First Event",
        videogame : "Videogame ONE",
        begin_at : plain_timestamp,
        end_at : plain_timestamp + 4096,
        modified_at : plain_timestamp,
        opponents : { team_one : "Team ONE", team_two : "Team TWO"},
        game_status : Ongoing()
    };

const secondary_event : Types.event_type =
    {
        name : "Secondary Event",
        videogame : "Videogame TWO",
        begin_at : plain_timestamp,
        end_at : plain_timestamp + 4096,
        modified_at : plain_timestamp,
        opponents : { team_one : "Team THREE", team_two : "Team FOUR"},
        game_status : Ongoing()
    };

const bootstrap_oracle = () => {
    // Boostrapping accounts
    let _ = Test.reset_state(6 as nat, (list([]) as list<tez>));
    let _baker: address = Test.nth_bootstrap_account(0);
    let elon: address = Test.nth_bootstrap_account(1);
    let jeff: address = Test.nth_bootstrap_account(2);
    let alice: address = Test.nth_bootstrap_account(3);
    let bob: address = Test.nth_bootstrap_account(4);
    let james: address = Test.nth_bootstrap_account(5);


    // Boostrapping storage
    let init_storage : Types.storage = {
        isPaused : false,
        manager : elon,
        signer : jeff,
        events : (Map.empty as map<nat, Types.event_type>),
        events_index : 0 as nat,
        metadata : (Map.empty as map<string, bytes>)
    };

    // Boostrapping Oracle contract 
    let oracle_path = "src/contracts/cameligo/oracle/main.mligo";
    let iBis = Test.run( (x : Types.storage) => x, init_storage);
    let [oracle_address, _, _] = Test.originate_from_file(oracle_path, "main", (list(["getManager", "getSigner", "getStatus", "getEvent"]) as list<string>), iBis, 0 as mutez);
    let oracle_taddress = (Test.cast_address(oracle_address) as typed_address<Types.action,Types.storage>);
    let oracle_contract = Test.to_contract(oracle_taddress);
    
    [oracle_contract, oracle_taddress, elon, jeff, alice, bob, james]
};

const bootstrap_oracle_callback = () => {
    let oracle_callback = Helper_Callback.originate_from_file(Helper_Callback.base_storage);   
    oracle_callback
};